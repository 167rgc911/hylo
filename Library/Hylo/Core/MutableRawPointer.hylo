/// A raw pointer for accessing and manipulating untyped data.
public type MutableRawPointer {

  var value: Builtin.ptr

  memberwise init

  /// Creates an instance referencing the same memory as `other`.
  public init(mutating other: RawPointer) {
    &self.value = other.value
  }

  /// Creates a null pointer.
  public static fun null() -> Self {
    .new(value: Builtin.zeroinitializer_ptr())
  }

  /// Returns `true` if `self` is equal to `other`. Otherwise, returns `false`.
  public fun infix== (_ other: Self) -> Bool {
    Bool(value: Builtin.icmp_eq_ptr(value, other.value))
  }

  /// Returns `true` if `self` is not equal to `other`. Otherwise, returns `false`.
  public fun infix!= (_ other: Self) -> Bool {
    Bool(value: Builtin.icmp_ne_ptr(value, other.value))
  }

}

public conformance MutableRawPointer: Deinitializable {}

public conformance MutableRawPointer: Movable {}
